# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=python-pylint
pkgver=2.8.2
pkgrel=1
pkgdesc="Analyzes Python code looking for bugs and signs of poor quality"
arch=('any')
url="https://pylint.org"
license=('GPL')
depends=('python-astroid' 'python-mccabe' 'python-isort' 'python-setuptools' 'python-toml')
makedepends=('python-pytest-runner' 'python-pytest-benchmark' 'python-setuptools-scm'
             'python-sphinx')
checkdepends=('mpdecimal')
optdepends=('tk: Pylint GUI'
            'graphviz: To have other output formats than dot or vcg')
source=("https://github.com/PyCQA/pylint/archive/v$pkgver/pylint-$pkgver.tar.gz")
sha512sums=('7023603668f540ecc0b35f3289377855c4c0df01e04a9af255f5c8031f568f6b76508bb3a7f0aba8e76a86b63eae423908c30f47258d6dee46bb23320c952701')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pylint-$pkgver
  python setup.py build
  make PYTHONPATH="$PWD/build/lib" -C doc man
}

check() {
  cd pylint-$pkgver
  python setup.py pytest || echo "Tests failed"
}

package() {
  cd pylint-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1

  install -d "$pkgdir"/usr/share/man/man1
  install -m644 doc/_build/man/* "$pkgdir"/usr/share/man/man1
}
