# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-astroid
pkgver=2.6.2
pkgrel=1
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="https://github.com/PyCQA/astroid"
license=('LGPL')
depends=('python-six' 'python-lazy-object-proxy' 'python-wrapt')
makedepends=('python-setuptools-scm')
checkdepends=('python-dateutil' 'python-pytest' 'python-numpy' 'python-nose' 'python-pytest-runner')
replaces=('python-logilab-astng')
conflicts=('python-logilab-astng')
source=(https://github.com/PyCQA/astroid/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('776f8f9a12d425b7f048318b632dedb8894b6a3051a0607d1079437084aab4e9593e39cf9c7e01c2dd722364d2396cf13d7d26cc2acf33482d75584c458d4b89')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cd astroid-$pkgver
  sed -i -e 's/six~=[0-9.*]*/six/;s/wrapt~=[0-9.*]*/wrapt/;s/lazy_object_proxy==[0-9.*]*/lazy_object_proxy/' astroid/__pkginfo__.py
}

build() {
  cd astroid-$pkgver
  python setup.py build
}

check() {
  cd astroid-$pkgver
  pytest
}

package() {
  cd astroid-$pkgver
  python setup.py install --optimize=1 --skip-build --prefix=/usr --root="$pkgdir"
}
