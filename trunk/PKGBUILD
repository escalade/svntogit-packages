# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-astroid
pkgver=2.6.0
pkgrel=1
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="https://github.com/PyCQA/astroid"
license=('LGPL')
depends=('python-six' 'python-lazy-object-proxy' 'python-wrapt')
makedepends=('python-setuptools-scm')
checkdepends=('python-dateutil' 'python-pytest' 'python-numpy' 'python-nose' 'python-pytest-runner')
replaces=('python-logilab-astng')
conflicts=('python-logilab-astng')
source=(https://github.com/PyCQA/astroid/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('44e0833e078948eb1ba1e62710890be8cfc675f7ee136578a39e2914b082fcfbcb90b90a002998471fcd19c4092b9a3c5f71e58af32d3839fa799bcb5bf4a4ba')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cd astroid-$pkgver
  sed -i -e 's/six~=[0-9.*]*/six/;s/wrapt~=[0-9.*]*/wrapt/;s/lazy_object_proxy==[0-9.*]*/lazy_object_proxy/' astroid/__pkginfo__.py
}

build() {
  cd astroid-$pkgver
  python setup.py build
}

check() {
  cd astroid-$pkgver
  pytest
}

package() {
  cd astroid-$pkgver
  python setup.py install --optimize=1 --skip-build --prefix=/usr --root="$pkgdir"
}
