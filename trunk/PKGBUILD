# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=python-pylint
pkgver=2.8.1
pkgrel=1
pkgdesc="Analyzes Python code looking for bugs and signs of poor quality"
arch=('any')
url="https://pylint.org"
license=('GPL')
depends=('python-astroid' 'python-mccabe' 'python-isort' 'python-setuptools' 'python-toml')
makedepends=('python-pytest-runner' 'python-pytest-benchmark' 'python-sphinx')
checkdepends=('mpdecimal')
optdepends=('tk: Pylint GUI'
            'graphviz: To have other output formats than dot or vcg')
source=("https://github.com/PyCQA/pylint/archive/pylint-$pkgver.tar.gz")
sha512sums=('ec8710396f07cdec8a395748ffda403a1f3fbd00094a1ad32e976c52be946c08b9e32521205dc165a0284467c68d9f467be40d141907bcd148b94e285eeca606')

build() {
  cd pylint-pylint-$pkgver
  python setup.py build
  make PYTHONPATH="$PWD/build/lib" -C doc man
}

check() {
  cd pylint-pylint-$pkgver
  # https://github.com/PyCQA/pylint/issues/3895
  python setup.py pytest || echo "Tests failed"
}

package() {
  cd pylint-pylint-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1

  install -d "$pkgdir"/usr/share/man/man1
  install -m644 doc/_build/man/* "$pkgdir"/usr/share/man/man1
}
